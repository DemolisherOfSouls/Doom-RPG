Actor Weapon_Striker : Weapon_BaseShotgun
{
	Weapon.AmmoType1 "AmmoMag_Striker"
	
	Inventory.Icon "GAsgA0"
	
	Tag "Striker"
	
	States
	{
	
	//******************************************************
	//	Ready / Select
	//******************************************************
	
	Ready:
		ASGG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	Select:
		ASGG A 0
		Goto Super::Select

	//******************************************************
	//	Fire
	//******************************************************
	
	Fire:
		ASGG A 1 A_JumpIfInventory("AmmoMag_Striker", 1, "Fire.HasAmmo")
		ASGG A 5 A_PlayWeaponSound("Weapon/StrikerEmpty")
		Goto Reload
	Fire.HasAmmo:
		ASGG A 3
		ASGG L 3 A_GunFlash("Shoot")
		ASGG MN 2
		ASGG A 2 A_ReFire("Fire")
		ASGG BCBA 2
		Goto Ready
	
	//******************************************************
	//	Fire - Actual
	//******************************************************
	
	Shoot:
		TNT1 A 0 A_PlayWeaponSound("Weapon/Striker")
		TNT1 A 0 A_TakeInventory("AmmoMag_Striker", 1)
		TNT1 A 0 A_AlertMonsters(640)
		TNT1 A 0 A_SetArg(2, CallACS("WeaponAccuracy", WT_SHOTGUN, 29))
		TNT1 AAAAAA 0 A_FireCustomMissile("WeaponProj_Striker", FRandom(-args[2], args[2]) / 10, false, 0, 0, 0, FRandom(-args[2], args[2]) / 10)
	Flash:
		TNT1 A 3 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	
	//******************************************************
	//	Reload
	//******************************************************
	
	Reload:
		ASGG A 1 A_JumpIfInventory("AmmoMag_Striker", 0, "Delay")
		ASGG A 0 A_JumpIfInventory("Ammo_Shell", 1, "Reload.Start")
		ASGG A 0 A_JumpIfInventory("AmmoMag_Striker", 1, "Ready")
		Goto ChangeWeapon
	Reload.Start:
		ASGG AABBCCDEFGHIJK 1
		ASGG K 35 A_SetTics(CallACS("Skill_Reloader", 35))
		ASSG K 0 A_GunFlash("Reload.Process")
		ASGG KJIHGFEDCCBBAA 1
		Goto Ready
	
	//******************************************************
	//	Reload - Process
	//******************************************************
	
	Reload.Process:
		TNT1 A 0 A_GiveInventory("AmmoMag_Striker", 1)
		TNT1 A 0 A_TakeInventory("Ammo_Shell", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("AmmoMag_Striker", 0, "Reload.Process.Done")
		TNT1 A 0 A_JumpIfInventory("Ammo_Shell", 1, "Reload.Process")
	Reload.Process.Done:
		TNT1 A 0 A_PlayWeaponSound("Weapon/StrikerEmpty")
		Stop
	}
}