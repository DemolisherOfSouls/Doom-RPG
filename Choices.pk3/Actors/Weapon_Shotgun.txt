Actor Weapon_Shotgun : Weapon_Base
{
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 4
	
	Weapon.AmmoType1 "Ammo_Shell"
	Weapon.AmmoType2 "AmmoMag_Shotgun"
	
	Tag "Shotgun"
	
	States
	{
	Ready:
		SHTG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	Select:
		SHTG A 1 A_Raise
		Loop
	Select.Quick:
		SHTG A 0 A_Raise
		SHTG A 1 A_Raise
		Loop
	
	Fire:
		SHTG A 1 A_JumpIfInventory("AmmoMag_Shotgun", 1, "Fire.HasAmmo")		//Continue if has 1 shell in shotgun
		SHTG A 5 A_PlaySound("Weapon/ShotgunEmpty", CHAN_WEAPON)
		Goto Reload	
	Fire.HasAmmo:
		SHTG A 3
		SHTG A 7 A_GunFlash("Shoot")
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		Goto Ready
	Shoot:
		SHTF A 0 A_PlaySound("Weapon/Shotgun", CHAN_WEAPON)
/*7*/	SHTF AAAAAAA 0 A_FireCustomMissile("WeaponProj_Shotgun", FRandom(-2.7,2.7), false, 0, 0, 0, FRandom(-2.7,2.7))
		SHTF A 0 A_TakeInventory("AmmoMag_Shotgun", 1)
		Goto Flash
	Flash:
		SHTF A 4 Bright A_Light1
		SHTF B 3 Bright A_Light2
		Goto LightDone
	
	Reload:
		SHTG A 1 A_JumpIfInventory("Ammo_Shell", 1, "Reload.Start")				//Continue if has 1 shell in ammo pool
		Goto Ready
	Reload.Start:
		SHTG A 0 A_JumpIfInventory("AmmoMag_Shotgun", 0, "Ready")			//Continue if shotgun is not full
		SHTG ABCD 4 //Start anim (before ammo swap)
	Reload.HasAmmo:
		//Per item anim (during ammo swap)
		SHTG D 1 Offset(0,48)
		SHTG D 1 Offset(0,52)
		SHTG D 1 Offset(0,56)
		SHTG D 1 Offset(0,60)
		SHTG D 0 A_PlaySound("Weapon/ShotgunLoad", Chan_Weapon)
		SHTG D 1 Offset(0,64)
		SHTG D 1 Offset(0,60)
		SHTG D 1 Offset(0,56)
		SHTG D 1 Offset(0,52)
		SHTG D 1 Offset(0,48)
		SHTG D 0 A_GiveInventory("AmmoMag_Shotgun", 1)
		SHTG D 0 A_TakeInventory("Ammo_Shell", 1, TIF_NOTAKEINFINITE)
		SHTG D 0 A_JumpIfInventory("AmmoMag_Shotgun", 0, "Reload.Finish")	//Continue if shotgun is not full
		SHTG D 0 A_JumpIfInventory("Ammo_Shell", 1, "Reload.HasAmmo")				//Continue if has 1 shell in ammo pool
		Goto Reload.Finish
	Reload.Finish:
		SHTG DCBA 4 //Finish anim (after ammo swap)
		Goto Ready
	}
}