Actor Weapon_Shotgun : Weapon_BaseShotgun
{
	Weapon.AmmoType1 "AmmoMag_Shotgun"
	
	Inventory.Icon "SHOTA0"
	
	Tag "Shotgun"
	
	States
	{
	Select:
		SHTG A 0
		Goto Super::Select
	
	Ready:
		SHTG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	
	Fire:
		SHTG A 3 A_JumpIfNoAmmo("Reload.Click")
		SHTG A 7 A_GunFlash("Shoot")
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		Goto Ready
	
	Shoot:
		SHTF A 0 A_TakeInventory("AmmoMag_Shotgun", 1)
		SHTF A 0 A_AlertMonsters(640)
		SHTF A 0 A_PlayWeaponSound("Weapon/Shotgun")
		SHTF A 0 A_SetArg(2, CallACS("WeaponAccuracy", WT_SHOTGUN, 27))
		SHTF A 0 A_JumpIfInventory("InvMod_Shotgun_Shot", 1, "Shoot.Big")
		SHTF AAAAAAA 0 A_FireCustomMissile("WeaponProj_Shotgun", FRandom(-args[2], args[2]) / 10, false, 0, 0, 0, FRandom(-args[2], args[2]) / 10)
		Goto Flash
	Shoot.Big:
		SHTF AAAAAAAAAA 0 A_FireCustomMissile("WeaponProj_Shotgun", FRandom(-args[2], args[2]) / 10, false, 0, 0, 0, FRandom(-args[2], args[2]) / 10)
	Flash:
		SHTF A 4 Bright A_Light1
		SHTF B 3 Bright A_Light2
		Goto LightDone
	
	Reload.CheckMag:
		SHTG A 0 A_JumpIfInventory("AmmoMag_Shotgun", 0, "Delay")
	Reload.Start:
		SHTG ABCD 2
	Reload.HasAmmo:
		SHTG D 1 Offset(0,48)
		SHTG D 1 Offset(0,52)
		SHTG D 1 Offset(0,56)
		SHTG D 1 Offset(0,60)
		SHTG D 1 Offset(0,64) A_PlayWeaponSound("Weapon/ShotgunLoad")
		SHTG D 1 Offset(0,60)
		SHTG D 1 Offset(0,56)
		SHTG D 1 Offset(0,52)
		SHTG D 1 Offset(0,48)
		SHTG D 0 A_GiveInventory("AmmoMag_Shotgun", 1)
		SHTG D 0 A_TakeInventory("Ammo_Shell", 1, TIF_NOTAKEINFINITE)
		SHTG D 0 A_ReFire("Reload.Finish")
		SHTG D 3 A_SetTics(CallACS("Skill_Reloader", 3))
		SHTG D 0 A_JumpIfInventory("AmmoMag_Shotgun", 0, "Reload.Finish")
		SHTG D 0 A_JumpIfInventory("Ammo_Shell", 1, "Reload.HasAmmo")
	Reload.Finish:
		SHTG DCBA 2
		Goto Ready
	}
}