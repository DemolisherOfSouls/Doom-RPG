Actor Weapon_MkII : Weapon_Base
{
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 5
	
	Weapon.AmmoType1 "Ammo_Clip"
	Weapon.AmmoType2 "AmmoMag_MkII"
	
	Tag "MkII Sidearm"
	
	+WEAPON.WIMPY_WEAPON
	
	States
	{
	Ready:
		MK2G A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	Select:
		MK2G A 1 A_Raise
		Loop
	Select.Quick:
		MK2G A 0 A_Raise
		MK2G A 1 A_Raise
		Loop
	
	Fire:
		MK2G A 1 A_JumpIfInventory("AmmoMag_MkII", 1, "Fire.HasAmmo")		//Continue if has 1 bullet in magazine
		MK2G A 4 A_PlaySound("Weapon/PistolEmpty", CHAN_WEAPON)
		Goto Reload
	Fire.HasAmmo:
		MK2G A 2
		MK2G B 5 A_GunFlash("Shoot")
		MK2G C 4
		MK2G B 4 A_ReFire("Fire")
		Goto Ready
	Shoot:
		MK2F A 0 A_PlaySound("Weapon/MkII", CHAN_WEAPON)
		MK2F A 0 A_TakeInventory("AmmoMag_MkII", 1)
		MK2F A 0 A_FireCustomMissile("WeaponProj_MkII", FRandom(-2.1,2.1), false, 0, 0, 0, FRandom(-2.1,2.1))
	Flash:
		MK2F A 5 Bright A_Light1
		Goto LightDone
	
	Reload:
		MK2G A 1 A_JumpIfInventory("Ammo_Clip", 1, "Reload.Start")				//Continue if has 1 bullet in ammo pool
		Goto Ready
	Reload.Start:
		MK2G A 0 A_JumpIfInventory("AmmoMag_MkII", 0, "Ready")			//Continue if magizine is not full
		MK2G DEFGHIJKLMNOPQRS 2 //Start anim (before ammo swap)
	Reload.HasAmmo:
		MK2G A 0 //Per item anim (during ammo swap)
		MK2G A 0 A_GiveInventory("AmmoMag_MkII", 1)
		MK2G A 0 A_TakeInventory("Ammo_Clip", 1, TIF_NOTAKEINFINITE)
		MK2G A 0 A_JumpIfInventory("AmmoMag_MkII", 0, "Reload.Finish")	//Continue if magizine is not full
		MK2G A 0 A_JumpIfInventory("Ammo_Clip", 1, "Reload.HasAmmo")				//Continue if has 1 bullet in ammo pool
		Goto Reload.Finish
	Reload.Finish:
		MK2G A 1 //Finish anim (after ammo swap)
		Goto Ready
	}
}