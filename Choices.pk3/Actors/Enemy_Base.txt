Actor Enemy_Base
{
	Height 56
	Monster
	+FLOORCLIP
	+DONTHARMSPECIES
	
	var int user_rewardxp;
	var int user_rewardrenown;
	
	//var int user_level;
	//var int user_health;
	
	var int user_deathtype;
	var int user_paintime;
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecuteAlways("EnemyTID")
		TNT1 A 0 A_Jump(Always, "Spawn.Stage1")
	Idle:
		"####" AAAAAAAAAABBBBBBBBBB 1 A_Look
		Loop
	Pain.Actual:
		"----" A 3 A_SetTics(user_paintime)
		"----" A 0 A_Pain
		"----" A 3 A_SetTics(user_paintime)
		"----" A 0 A_Jump(Always, "See")
	Death:
		"----" A 0 ACS_NamedExecuteAlways("EnemyDeath")
		"----" A 0 A_SetUserVar("user_deathtype", 1)
		"----" A 0 A_Jump(Always, "Death.Stage1")
	XDeath:
		"----" A 0 ACS_NamedExecuteAlways("EnemyDeath")
		"----" A 0 A_SetUserVar("user_deathtype", 2)
		"----" A 0 A_Jump(Always, "XDeath.Stage1")
	Death.Headshot:
		"----" A 0 ACS_NamedExecuteAlways("EnemyDeath", 0, true)
		"----" A 0 A_SetUserVar("user_deathtype", 5)
		"----" A 0 A_Jump(Always, "Headshot.Stage1")
	Death.Finish:
		"----" A -1 A_CheckFlag("BOSSDEATH", "Death.Finish.Boss")
		Stop
	Death.Finish.Boss:
		"----" A -1 A_BossDeath
		Stop
	Raise:
		"----" A 0 A_JumpIf(user_deathtype == 5, "Raise.Headshot")
		"----" A 0 A_JumpIf(user_deathtype == 2, "Raise.XDeath")
		"----" A 0 A_Jump(Always, "Raise.Death")
		
	}
}

Actor Enemy_Zombie : Enemy_Base
{
	Radius 20
	Mass 100
	Speed 4
	Species "Zombie"
	States
	{
	Spawn.Stage1:
		TNT1 A 0 A_SpawnItemEx("Enemy_ZombieHead", 0, 0, 41, 0, 0, 0, 0, SXF_HEADSPAWN)
		TNT1 A 0 A_SetUserVar("user_paintime", 3)
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	See:
		"####" AAAABBBBCCCCDDDD 2 A_Chase
		Loop
	Pain:
		"####" G 0 A_Jump(Always, "Pain.Actual")
	Death.Stage1:
		"####" H 5
		"####" I 5 A_ScreamAndUnblock
		"####" JK 5
		"####" L 0 A_Jump(Always, "Death.Finish")
	Headshot.Stage1:
	XDeath.Stage1:
		"####" M 5
		"####" N 5 A_XScream
		"####" O 5 A_NoBlocking
		"####" PQRST 5
		"####" U 0 A_Jump(Always, "Death.Finish")
	Raise.Death:
		"####" LKJIH 5
		"####" H 0 A_Jump(Always, "See")
	Raise.Headshot:
	Raise.XDeath:
		"####" UTSRQPONM 5
		"####" M 0 A_Jump(Always, "See")
	}
}

Actor Enemy_Demonic : Enemy_Base
{
	Speed 8
	Species "Demon"
	+MISSILEMORE
	States
	{
	Spawn.Stage1:
		TNT1 A 0 A_SpawnItemEx("Enemy_DemonicHead", 0, 0, 41, 0, 0, 0, 0, SXF_HEADSPAWN)
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	}
}

Actor Enemy_Baron : Enemy_Demonic
{
	Radius 24
	Height 64
	Mass 1000
	PainChance 50
	BloodColor "Green"
	States
	{
	Spawn.Stage1:
		TNT1 A 0 A_SpawnItemEx("Enemy_BaronHead", 0, 0, 49, 0, 0, 0, 0, SXF_HEADSPAWN)
		TNT1 A 0 A_SetUserVar("user_paintime", 2)
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	See:
		"####" AABBCCDD 3 A_Chase("Attack", "Attack")
		Loop
	Pain:
		"####" H 0 A_Jump(Always, "Pain.Actual")
	Headshot.Stage1:
	XDeath.Stage1:
	Death.Stage1:
		"####" I 8
		"####" J 8 A_Scream
		"####" K 8
		"####" L 8 A_NoBlocking
		"####" MN 8
		"####" O 0 A_Jump(Always, "Death.Finish")
	Raise.Headshot:
	Raise.XDeath:
	Raise.Death:
		"####" ONMLKJI 8
		"####" I 0 A_Jump(Always, "See")
	}
}

Actor Enemy_Cyborg : Enemy_Base
{
	Species "Cyborg"
	+NOFEAR
	+MISSILEEVENMORE
	States
	{
	Spawn.Stage1:
		TNT1 A 0 A_SpawnItemEx("Enemy_CyborgHead", 0, 0, 41, 0, 0, 0, 0, SXF_HEADSPAWN)
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	}
}