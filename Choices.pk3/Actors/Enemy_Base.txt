Actor Enemy_Base : Entity_Base
{
	var int user_paintime;
	var int user_painjump;
	
	States
	{
	Spawn:
	Spawn.Final:
		"####" A 0 NoDelay ACS_NamedExecuteAlways("EnemySetup")
	Idle:
		"####" AAAAAAAAAABBBBBBBBBB 1 A_Look
		Loop
	See:
		"----" A 0 A_SetUserVar("user_awake", true)
		"----" A 0 A_SetUserVar("user_alert", true)
		"----" A 0 A_JumpIf(user_painjump, "See.NoDelay")
		"----" A 0 A_Jump(Always, "See.Real")
	Pain.Actual:
		"----" A 3 A_SetTics(user_paintime)
		"----" A 0 A_Pain
		"----" A 3 A_SetTics(user_paintime)
		"----" A 0 A_Jump(Always, "See")
	Death:
		"----" A 0 A_SetUserVar("user_deathtype", 1)
		"----" A 0 ACS_NamedExecuteAlways("EnemyDeath")
		"----" A 0 A_Jump(Always, "Death.Stage1")
	XDeath:
		"----" A 0 A_SetUserVar("user_deathtype", 2)
		"----" A 0 ACS_NamedExecuteAlways("EnemyDeath")
		"----" A 0 A_Jump(Always, "XDeath.Stage1")
	Death.Headshot:
		"----" A 0 A_SetUserVar("user_deathtype", 5)
		"----" A 0 ACS_NamedExecuteAlways("EnemyDeath")
		"----" A 0 A_Jump(Always, "Headshot.Stage1")
	Death.Finish:
		"----" A -1 A_CheckFlag("BOSSDEATH", "Death.Finish.Boss")
		Stop
	Death.Finish.Boss:
		"----" A -1 A_BossDeath
		Stop
	}
}

Actor Enemy_Zombie : Enemy_Base
{
	Radius 20
	Mass 100
	Speed 4
	Species "Zombie"
	DamageFactor "Enemy_PistolBullet", 0
	DamageFactor "Enemy_RifleBullet", 0
	DamageFactor "Enemy_ShotgunPellet", 0
	DamageFactor "Enemy_ChaingunBullet", 0
	States
	{
	Spawn:
		TNT1 A 0 A_SetUserVar("user_paintime", 3)
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	See.Real:
		"####" AAAABBBBCCCCDDDD 2 A_Chase
		Loop
	Pain:
		"####" G 0 A_Jump(Always, "Pain.Actual")
	Death.Stage1:
		"####" H 5
		"####" I 5 A_ScreamAndUnblock
		"####" JKL 5
		Goto Death.Finish
	Headshot.Stage1:
	XDeath.Stage1:
		"####" M 5
		"####" N 5 A_XScream
		"####" O 5 A_NoBlocking
		"####" PQRSTU 5
		Goto Death.Finish
	Raise.Death:
		"####" LKJIH 5
		Goto See
	Raise.Headshot:
	Raise.XDeath:
		"####" UTSRQPONM 5
		Goto See
	}
}

Actor Enemy_Demonic : Enemy_Base
{
	Speed 8
	Species "Demon"
	DamageFactor "Enemy_ImpBall", 0
	DamageFactor "Enemy_CacoBall", 0
	DamageFactor "Enemy_BaronBall", 0
	DamageFactor "Enemy_HellKnightBall", 0
	+MISSILEMORE
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	}
}

Actor Enemy_DemonicFloater : Enemy_Demonic
{
	+FLOAT
	+NOGRAVITY
	+DONTFALL
}

Actor Enemy_Baron : Enemy_Demonic
{
	Radius 24
	Height 64
	Mass 1000
	PainChance 50
	BloodColor "Green"
	States
	{
	Spawn:
		TNT1 A 0 A_SetUserVar("user_paintime", 2)
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	See.Real:
		"####" AABBCCDD 3 A_Chase("Attack", "Attack")
		Loop
	Pain:
		"####" H 0 A_Jump(Always, "Pain.Actual")
	Headshot.Stage1:
	XDeath.Stage1:
	Death.Stage1:
		"####" I 8
		"####" J 8 A_ScreamAndUnBlock
		"####" KLMN 8
		Goto Death.Finish
	Raise.Headshot:
	Raise.XDeath:
	Raise.Death:
		"####" ONMLKJI 8
		Goto See
	}
}

Actor Enemy_Cyborg : Enemy_Base
{
	Species "Cyborg"
	DamageFactor "Enemy_ArachPlasma", 0
	DamageFactor "Enemy_MancubusShot", 0
	DamageFactor "Enemy_RevenantSeeker", 0
	DamageFactor "Enemy_SpiderBullet", 0
	DamageFactor "Enemy_CyberdemonRocket", 0
	+NOFEAR
	+MISSILEEVENMORE
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(Always, "Spawn.Stage2")
	}
}