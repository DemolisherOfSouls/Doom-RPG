Actor Weapon_Riotgun : Weapon_BaseShotgun
{
	Weapon.AmmoType1 "AmmoMag_Riotgun"
	
	Inventory.Icon "GRsgA0"
	
	Tag "Riot Shotgun"
	
	States
	{
	Ready:
		RsgG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	Select:
		RsgG A 0
		Goto Super::Select
	
	Fire:
		RsgG A 3 A_JumpIfNoAmmo("Reload.Click")
		RsgG A 7 A_GunFlash("Shoot")
		RsgG BCDCB 4
		RsgG A 3
		Goto Ready
	
	Shoot:
		RsgG E 0 A_TakeInventory("AmmoMag_Riotgun", 1)
		RsgG E 0 A_AlertMonsters(608)
		RsgG E 0 A_JumpIfInventory("InvMod_Riotgun_Choke", 1, "Shoot.Choke")
		RsgG E 0 A_SetArg(2, CallACS("WeaponAccuracy", WT_SHOTGUN, 23))
		Goto Flash
	Shoot.Choke:
		RsgG E 0 A_SetArg(2, CallACS("WeaponAccuracy", WT_SHOTGUN, 19))
	Flash:
		RsgG EEEEEE 0 A_FireCustomMissile("WeaponProj_Riotgun", FRandom(-args[2], args[2]) / 10, false, 0, 0, 0, FRandom(-args[2], args[2]) / 10)
		RsgG E 2 Bright A_Light1
		RsgG F 2 Bright A_Light2
		RsgG G 2 Bright A_Light1
		Goto LightDone
	
	Reload.Start:
		RsgG A 2 A_JumpIfInventory("AmmoMag_Riotgun", 0, "Delay")
		RsgG BCD 2
	Reload.HasAmmo:
		RsgG D 1 Offset(0,48)
		RsgG D 1 Offset(0,53)
		RsgG D 1 Offset(0,58)
		RsgG D 1 Offset(0,63) A_PlayWeaponSound("Weapon/ShotgunLoad")
		RsgG D 1 Offset(0,58)
		RsgG D 1 Offset(0,53)
		RsgG D 1 Offset(0,48)
		RsgG D 0 A_GiveInventory("AmmoMag_Riotgun", 1)
		RsgG D 0 A_TakeInventory("Ammo_Shell", 1, TIF_NOTAKEINFINITE)
		RsgG D 0 A_ReFire("Reload.Finish")
		RsgG D 2 A_SetTics(CallACS("Skill_Reloader", 2))
		RsgG D 0 A_JumpIfInventory("AmmoMag_Riotgun", 0, "Reload.Finish")
		RsgG D 0 A_JumpIfInventory("Ammo_Shell", 1, "Reload.HasAmmo")
	Reload.Finish:
		RsgG DCBA 2
		Goto Ready
	}
}