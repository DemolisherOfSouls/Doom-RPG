Actor Weapon_SuperShotgun : Weapon_Base
{
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 2
	
	Weapon.AmmoType2 "Ammo_Shell"
	Weapon.AmmoType1 "AmmoMag_SuperShotgun"
	
	Inventory.Icon "SGN2A0"
	
	Tag "Super Shotgun"
	
	States
	{
	Ready:
		SHT2 A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	Select:
		SHT2 A 0
		Goto Super::Select
	
	Fire:
		SHT2 A 1 A_JumpIfInventory("AmmoMag_SuperShotgun", 2, "Fire.HasAmmo")
		SHT2 A 5 A_PlayWeaponSound("Weapon/SuperShotgunEmpty")
		Goto Reload
	Fire.HasAmmo:
		SHT2 A 3
		SHT2 A 7 A_GunFlash("Shoot")
		SHT2 BC 7
		Goto Reload.Force
	
	Shoot:
		SHT2 I 0 A_PlayWeaponSound("weapons/sshotf")
		SHT2 I 0 A_TakeInventory("AmmoMag_SuperShotgun", 2)
		SHT2 IIIIIIIIIIIIIIIIIIII 0 A_FireCustomMissile("WeaponProj_SuperShotgun", FRandom(-3.5,3.5), False, 0, 0, 0, FRandom(-2.8,2.8))
		SHT2 I 0 A_AlertMonsters(768)
	Flash:
		SHT2 I 4 Bright A_Light1
		SHT2 J 3 Bright A_Light2
		Goto LightDone
	
	Reload:
		SHT2 A 1 A_JumpIfInventory("Ammo_Shell", 2, "Reload.Restart")
		SHT2 A 0 A_JumpIfNoAmmo("ChangeWeapon")
		Goto Delay
	Reload.Force:
		SHT2 C 0 A_JumpIfInventory("Ammo_Shell", 2, "Reload.Start")
		Goto ChangeWeapon
	Reload.Restart:
		SHT2 A 0 A_JumpIfInventory("AmmoMag_SuperShotgun", 0, "Ready")
		SHT2 BC 7
	Reload.Start:
		SHT2 C 0 A_JumpIfInventory("AmmoMag_SuperShotgun", 0, "Ready")
		SHT2 D 6 A_OpenShotgun2
		SHT2 E 7 A_TakeInventory("Ammo_Shell", 2, TIF_NoTakeInfinite)
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6 A_GiveInventory("AmmoMag_SuperShotgun", 2)
		SHT2 H 6 A_CloseShotgun2
		SHT2 A 5 A_ReFire("Fire")
		Goto Ready
	}
}