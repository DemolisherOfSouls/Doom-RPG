//Needs SKILL_Reloader implementation
//Needs Accuracy implementation

Actor Weapon_PlasmaRifle : Weapon_Base
{
	Weapon.SlotNumber 6
	Weapon.SelectionOrder 1
	
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Ammo_Cell"
	Weapon.AmmoType1 "AmmoMag_PlasmaRifle"
	
	Inventory.Icon "PLASA0"
	
	Tag "Plasma Rifle"
	
	States
	{
	Ready:
		PLSG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	Select:
		PLSG A 0
		Goto Super::Select
	
	Fire:
		PLSG A 0 A_JumpIfNoAmmo("Reload")
		PLSG A 3 A_GunFlash("Fire.Actual")
		Goto Ready
	
	Fire.Actual:
		PLSF A 0 A_FireCustomMissile("WeaponProj_PlasmaBall", FRandom(-0.5, 0.5), true, 0, 0, 0, FRandom(-0.5, 0.5))
		PLSF A 0 A_AlertMonsters(640)
		PLSF A 0 A_JumpIf(Always, "Flash.A", "Flash.B")
	Flash.A:
		PLSF A 3 Bright A_Light1
		Goto LightDone
	Flash.B:
		PLSF B 3 Bright A_Light1
		Goto LightDone
	
	Reload:
		PLSG A 0 A_JumpIfInventory("Ammo_Cell", 1, "Reload.Start")
		PLSG A 0 A_JumpIfNoAmmo("ChangeWeapon")
		Goto Delay
	Reload.Start:
		PLSG B 20 A_JumpIfInventory("AmmoMag_PlasmaRifle", 0, "Delay")
		PLSG A 0 A_GiveInventory("ReloadPlasmaRifle", 1)
		Goto Ready
	}
}

Actor ReloadPlasmaRifle : Action_Base
{
	States
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("AmmoMag_PlasmaRifle", 1)
		TNT1 A 0 A_TakeInventory("Ammo_Cell", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("AmmoMag_PlasmaRifle", 0, "Done")
		TNT1 A 0 A_JumpIfInventory("Ammo_Cell", 1, "Pickup")
		Stop
	}
}