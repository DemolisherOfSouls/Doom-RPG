Actor Enemy_Imp : Enemy_Demonic replaces DoomImp
{
	SpawnID 5
	Health 60
	Radius 20
	Mass 100
	PainChance 200
	SeeSound "imp/sight"
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	
	DropItem "Item_Soul", 255
	DropItem "Item_Soul", 128
	DropItem "Item_Soul", 16

	States
	{
	Spawn.Stage2:
		TROO A 0 A_SetUserVar("user_rewardxp", 10)
		TROO A 0 A_SetUserVar("user_paintime", 2)
		TROO A 0 A_SetUserVar("user_type", ET_IMP)
		"####" A 0 A_Jump(Always, "Spawn.Final")
	See.Real:
		TROO AABBCCDD 3 A_Chase("Attack", "Attack")
		Loop
	Attack:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_CustomComboAttack("EnemyProj_ImpBall", 32, 3 * random(1, 8), "imp/melee", "Enemy_ImpMelee", true)
		Goto See
	Pain:
		TROO H 0 A_Jump(Always, "Pain.Actual")
	Death.Stage1:
		TROO I 8
		TROO J 8 A_ScreamAndUnblock
		TROO KL 6
		TROO M 0 A_Jump(Always, "Death.Finish")
	Headshot.Stage1:
		TROO V 8
		TROO W 8 A_ScreamAndUnblock
		TROO XY 6
		TROO Z 0 A_Jump(Always, "Death.Finish")
	XDeath.Stage1:
		TROO N 5
		TROO O 5 A_XScream
		TROO P 5
		TROO Q 5 A_NoBlocking
		TROO RST 5
		TROO U 0 A_Jump(Always, "Death.Finish")
	Raise.Death:
		TROO MLK 6
		TROO JI 8
		Goto See
	Raise.Headshot:
		TROO ZYX 6
		TROO WV 8
	Raise.XDeath:
		TROO UTSRQPON 5
		Goto See
	}
}