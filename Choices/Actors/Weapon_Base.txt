Actor Weapon_Base : Weapon
{
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	+INVENTORY.UNTOSSABLE
	+WEAPON.NOAUTOAIM
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NOALERT
	
	/*
	0 - (CHG - Current Speed)
	1 - (CHG - Tics in Frame, PIS - Firing Flags)
	2 - Accuracy
	3 - (CHG - Speed Offset before Firing)
	4 - (CHG - Slow Speed)
	*/
	
	States
	{
	
	//******************************************************
	//	ChangeWeapon
	//******************************************************
	
	ChangeWeapon:
		"----" A 0 ACS_NamedExecuteAlways("ChangeWeapon")
		"----" A 0 A_Jump(Always, "Ready.NoFire")
	
	
	//******************************************************
	//	CheckAmmo
	//******************************************************
	
	Ready.CheckAmmo:
		"----" A 0 A_JumpIfNoAmmo("ChangeWeapon")
		Goto Delay
	
	//******************************************************
	//	Ready / Delay
	//******************************************************
	
	Delay:
		"----" A 1
		"----" A 0 A_Jump(Always, "Ready")
	Ready:
		"----" A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
		Loop
	Ready.NoFire:
		"----" A 1 A_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD)
		Loop
	
	//******************************************************
	//	Select / Deselect
	//******************************************************
	
	Select:
		"----" A 0 A_JumpIf(CallACS("Skill_QuickDraw") == 1, "Select.Quick")
		"----" A 0 A_JumpIf(CallACS("Skill_QuickDraw") >= 2, "Select.Quick2")
		"----" A 1 A_Raise
		Wait
	Select.Quick:
		"----" A 0 A_Raise
		"----" A 1 A_Raise
		Loop
	Select.Quick2:
		"----" AAA 0 A_Raise
		"----" A 1 A_Raise
		Loop
		
	Deselect:
		"----" A 0 A_JumpIf(CallACS("Skill_QuickDraw") == 1, "Deselect.Quick")
		"----" A 0 A_JumpIf(CallACS("Skill_QuickDraw") >= 2, "Deselect.Quick2")
		"----" A 1 A_Lower
		Wait
	Deselect.Quick:
		"----" A 0 A_Lower
		"----" A 1 A_Lower
		Loop
	Deselect.Quick2:
		"----" AAA 0 A_Lower
		"----" A 1 A_Lower
		Loop
	
	//******************************************************
	//	Filler
	//******************************************************
	
	Fire:
		"----" A 1
		Goto Reload
	}
}