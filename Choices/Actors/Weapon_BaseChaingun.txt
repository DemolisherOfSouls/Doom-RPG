Actor Weapon_BaseChaingun : Weapon_Base
{
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 3
	
	Weapon.AmmoType1 "Ammo_Clip"

	States
	{
	
	//******************************************************
	//	SpinDown
	//******************************************************

	SpinDown.B.Drop:
		"####" B 0 A_JumpIf(args[0] == 0, "Ready.CheckAmmo")
		"####" B 0 A_SetArg(0, args[0] - 1)
	SpinDown.B:
		"####" B 0 A_SetArg(1, 0)
		"####" B 0 A_PlayWeaponSound("Weapon/ChaingunEmpty")
		"####" B 1 A_WeaponReady
		"####" B 0 A_SetArg(1, args[1] + 1)
		"####" B 0 A_JumpIf(args[1] == args[4] - args[0], "SpinDown.A.Drop")
		Goto SpinDown.B + 2
	
	SpinDown.A.Drop:
		"####" A 0 A_JumpIf(args[0] == 0, "Ready.CheckAmmo")
		"####" A 0 A_SetArg(0, args[0] - 1)
	SpinDown.A:
		"####" A 0 A_SetArg(1, 0)
		"####" A 0 A_PlayWeaponSound("Weapon/ChaingunEmpty")
		"####" A 1 A_WeaponReady
		"####" A 0 A_SetArg(1, args[1] + 1)
		"####" A 0 A_JumpIf(args[1] == args[4] - args[0], "SpinDown.B.Drop")
		Goto SpinDown.A + 2
		
	//******************************************************
	//	CheckTrigger
	//******************************************************
		
	CheckTrigger.A:
		"####" A 0 A_JumpIf(CallACS("GetKeyPress", MODINPUT_BUTTONS, BT_ATTACK), "Hold")
		"####" A 0 A_ReFire("AltHold")
		Goto SpinDown.B
	CheckTrigger.B:
		"####" B 0 A_JumpIf(CallACS("GetKeyPress", MODINPUT_BUTTONS, BT_ATTACK), "Fire")
		"####" B 0 A_ReFire("AltFire")
		Goto SpinDown.A

	//******************************************************
	//	AltFire - SpinUp
	//******************************************************

	AltFire:
		"####" A 7 A_SetTics(args[4] - args[0])
		"####" A 0 A_JumpIf(args[0] == args[3], "CheckTrigger.A")
		Goto CheckTrigger.A
		
	AltHold:
		"####" B 7 A_SetTics(args[4] - args[0])
		"####" B 0 A_JumpIf(args[0] == args[3], "CheckTrigger.B")
		"####" B 0 A_SetArg(0, args[0] + 1)
		"####" B 0 A_JumpIf(args[0] == args[3], "CheckTrigger.B")
		Goto CheckTrigger.B
	
	//******************************************************
	//	Fire / Hold
	//******************************************************
	
	Fire:
		"####" A 0 A_JumpIf(args[0] == args[3], "Fire.CheckAmmo")
		"####" A 7 A_SetTics(args[4] - args[0])
		Goto CheckTrigger.A
	Fire.CheckAmmo:
		"####" A 0 A_JumpIfNoAmmo("Fire.NoAmmo")
		"####" A 0 A_GunFlash("Flash.A")
		"####" A 7 A_SetTics(args[4] - args[0])
		Goto CheckTrigger.A
	Fire.NoAmmo:
		"####" A 0 A_PlayWeaponSound("Weapon/ChaingunEmpty")
		"####" A 7 A_SetTics(args[4] - args[0])
		Goto SpinDown.B
		
	Hold:
		"####" B 0 A_JumpIf(args[0] == args[3], "Hold.CheckAmmo")
		"####" B 7 A_SetTics(args[4] - args[0])
		"####" B 0 A_SetArg(0, args[0] + 1)
		Goto CheckTrigger.B
	Hold.CheckAmmo:
		"####" A 0 A_JumpIfNoAmmo("Hold.NoAmmo")
		"####" B 0 A_GunFlash("Flash.B")
		"####" B 7 A_SetTics(args[4] - args[0])
		Goto CheckTrigger.B
	Hold.NoAmmo:
		"####" B 0 A_PlayWeaponSound("Weapon/ChaingunEmpty")
		"####" B 7 A_SetTics(args[4] - args[0])
		Goto SpinDown.A
		
	//******************************************************
	//	Fire - Actual
	//******************************************************
	
	Fire.Actual:
		"----" A 0 A_SetArg(2, CallACS("WeaponAccuracy", WT_CHAINGUN, 25))
		"----" A 0 A_AlertMonsters(640)
		"----" A 0 A_Light(Random(1,2))
		"----" A 0 A_Jump(Always, "Fire.Finish")
	}
}