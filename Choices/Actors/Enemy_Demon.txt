Actor Enemy_Demon : Enemy_Demonic replaces Demon
{
	SpawnID 8
	Health 150
	PainChance 180
	Speed 10
	Radius 30
	Mass 400
	SeeSound "demon/sight"
	AttackSound "demon/melee"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	Obituary "$OB_DEMONHIT"
	
	DropItem "Item_Soul", 255
	DropItem "Item_Soul", 255
	DropItem "Item_Soul", 160
	DropItem "Item_Soul", 32
	
	States
	{
	Spawn.Stage2:
		SARG A 0 A_SetUserVar("user_rewardxp", 15)
		SARG A 0 A_SetUserVar("user_paintime", 2)
		SARG A 0 A_SetUserVar("user_type", ET_DEMON)
		Goto Spawn.Final
	See.Real:
		SARG AABBCCDD 2 A_Chase
		Loop
	Melee:
		SARG EF 8 A_FaceTarget
		SARG G 8 A_SargAttack
		Goto See
	Pain:
		SARG H 0 A_Jump(Always, "Pain.Actual")
	XDeath.Stage1:
		SARG O 5
		SARG P 5 A_XScream
		SARG Q 5 A_NoBlocking
		SARG RSTUVW 5
		Goto Death.Finish
	Death.Stage1:
		SARG I 8
		SARG J 8 A_ScreamAndUnblock
		SARG KLMN 4
		Goto Death.Finish
	Raise.XDeath:
		SARG WVUTSRQPO 5
		Goto See
	Raise.Death:
		SARG NMLK 4
		SARG JI 8
		Goto See
	}
}