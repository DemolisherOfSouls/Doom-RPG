Actor Enemy_Demon : Enemy_Demonic replaces Demon
{
	SpawnID 8
	Health 150
	PainChance 180
	Speed 10
	Radius 30
	Mass 400
	SeeSound "demon/sight"
	AttackSound "demon/melee"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	Obituary "$OB_DEMONHIT"
	
	DropItem "Item_Soul", 255
	DropItem "Item_Soul", 255
	DropItem "Item_Soul", 160
	DropItem "Item_Soul", 32
	
	States
	{
	Spawn.Stage2:
		SARG A 0 A_SetUserVar("user_rewardxp", 15)
		SARG A 0 A_SetUserVar("user_paintime", 2)
		SARG A 0 A_SetUserVar("user_type", ET_DEMON)
		"####" A 0 A_Jump(Always, "Spawn.Final")
	See.Real:
		SARG AABBCCDD 2 A_Chase
		Loop
	Melee:
		SARG EF 8 A_FaceTarget
		SARG G 8 A_SargAttack
		Goto See
	Pain:
		SARG H 0 A_Jump(Always, "Pain.Actual")
	Headshot.Stage1:
	XDeath.Stage1:
	Death.Stage1:
		SARG I 8
		SARG J 8 A_ScreamAndUnblock
		SARG KLM 4
		SARG N 0 A_Jump(Always, "Death.Finish")
	Raise.Headshot:
	Raise.XDeath:
	Raise.Death:
		SARG NMLKJI 5
		Goto See
	}
}